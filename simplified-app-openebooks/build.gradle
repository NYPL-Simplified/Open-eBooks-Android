apply plugin: 'com.android.application'

def versionValues() {
  def propsFile = file("version.properties")
  def Properties props = new Properties()
  def name
  def code
  if (propsFile.canRead()) {
    props.load(new FileInputStream(propsFile))
    name = props["versionName"]
    code = props["versionCode"].toInteger()
  } else {
    throw new FileNotFoundException("Could not read version.properties!")
  }

  props["versionCode"] = (code + 1).toString()
  props.save(new FileOutputStream(propsFile), "")

  logger.info("incrementing build version ${code} -> ${code + 1}")
  return new Tuple(name, code)
}

android {
  def version = versionValues()

  compileSdkVersion 28
  buildToolsVersion "28.0.3"
  packagingOptions {
    exclude 'META-INF/LICENSE'

    doNotStrip "*/armeabi-v7a/*.so"
    doNotStrip '*/arm64-v8a/*.so'

    exclude ('/lib/mips/**')
    exclude ('/lib/mips64/**')
    exclude ('/lib/x86_64/**')
    exclude ('/lib/x86/**')

    // The PDF library and Readium both provide this shared library. This will
    // cause the build to fail because Gradle doesn"t know which one to pick.
    pickFirst "lib/arm64-v8a/libc++_shared.so"
    pickFirst "lib/armeabi-v7a/libc++_shared.so"
  }
  signingConfigs {
    release {
      keyAlias findProperty("org.librarysimplified.openebooks.keyAlias")
      keyPassword findProperty("org.librarysimplified.openebooks.keyPassword")
      storeFile file("keystore.jks")
      storePassword findProperty("org.librarysimplified.openebooks.storePassword")
    }
  }
  buildTypes {
    release {
      signingConfig signingConfigs.release
    }
  }
  lintOptions {
    checkReleaseBuilds false
  }

  compileSdkVersion androidCompileSDKVersion
  buildToolsVersion androidBuildToolsVersion

  defaultConfig {
    minSdkVersion androidMinimumSDKVersion
    targetSdkVersion androidTargetSDKVersion
    versionName = version.get(0)
    versionCode = version.get(1)
    setProperty("archivesBaseName", "openebooks-${versionName}-${versionCode}")
  }
}

description = 'simplified-app-openebooks'

dependencies {
  compile project(':simplified-app-shared')

  implementation "org.librarysimplified.drm.adobe:org.librarysimplified.drm.adobe.provider:1.1.27"
  implementation "org.librarysimplified.drm:org.librarysimplified.drm.core:1.1.2"
}

/*
 * Check that various required files exist.
 */

preBuild.doFirst {
  if (!file("${project.rootDir}/simplified-app-openebooks/keystore.jks").exists()) {
    throw new IllegalStateException("""
You have not added the required keystore at ${project.rootDir}/simplified-app-openebooks/keystore.jks.

Obtain it from the private NYPL credentials repository.
""")
  }

  if (!file("${project.rootDir}/simplified-app-openebooks/src/main/assets/ReaderClientCert.sig").exists()) {
    throw new IllegalStateException("""
You are attempting to build with Adobe DRM but have not added the required
certificate file at ${project.rootDir}/simplified-app-openebooks/src/main/assets/ReaderClientCert.sig.

Obtain it from the private NYPL credentials repository.
""")
  }

  if (!file("${project.rootDir}/simplified-app-openebooks/src/main/assets/bugsnag.conf").exists()) {
    throw new IllegalStateException("""
You are attempting to build with Bugsnag but have not added the required
configuration file at ${project.rootDir}/simplified-app-openebooks/src/main/assets/bugsnag.conf.

Obtain it from the private NYPL credentials repository.
""")
  }
}
